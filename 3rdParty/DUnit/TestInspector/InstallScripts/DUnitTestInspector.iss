; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "DUnitTestInspector"
#define VersionNumber "0.2"
#define MyAppPublisher "AEMO"

[Setup]
AppName={#MyAppName}
AppVerName={#MyAppName} {#VersionNumber}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
OutputBaseFilename={#MyAppName}_setup
Compression=lzma
SolidCompression=yes

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Files]
Source: ..\_bin\DUnitTestInspector.bpl; DestDir: {app}; Flags: ignoreversion; AfterInstall: AddToDelphi2007KnownPackages
Source: ..\..\..\ota\_bin\OTAUtils.bpl; DestDir: {commondocs}\RAD Studio\5.0\Bpl; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKCU; Subkey: Software\DUnitTestInspector; ValueType: expandsz; ValueName: WatchFile; ValueData: {userappdata}\DUnitTestInspector\{#VersionNumber}\watchFile; Flags: uninsdeletekey; AfterInstall: CreateWatchFileFolder

[Icons]
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}

[Code]
procedure AddBDS5KnownPackageRegValue(APackageFilePath, APackageDescription: string);
begin
   RegWriteStringValue(HKEY_CURRENT_USER, 'Software\Borland\BDS\5.0\Known Packages',
    APackageFilePath, APackageDescription);
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\Borland\BDS\5.0\Disabled Packages',
    APackageFilePath)
end;

procedure DeleteBDS5KnownPackageRegValue(APackageFilePath: string);
begin
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\Borland\BDS\5.0\Known Packages',
    APackageFilePath)
  RegDeleteValue(HKEY_CURRENT_USER, 'Software\Borland\BDS\5.0\Disabled Packages',
    APackageFilePath)
end;

procedure AddToDelphi2007KnownPackages;
begin
  AddBDS5KnownPackageRegValue(ExpandConstant('{app}\DUnitTestInspector.bpl'), 'DUnit Test Inspector')
end;

procedure DeleteFromDelphi2007KnownPackages;
begin
  DeleteBDS5KnownPackageRegValue(ExpandConstant('{app}\DUnitTestInspector.bpl'))
end;

procedure CreateWatchFileFolder();
begin
  ForceDirectories(ExpandConstant('{userappdata}\DUnitTestInspector\{#VersionNumber}'))
end;

procedure DeleteWatchFileFolder();
begin
  DelTree(ExpandConstant('{userappdata}\DUnitTestInspector'), true, true, true);
end;

procedure DeinitializeUninstall();
begin
  DeleteFromDelphi2007KnownPackages;
  DeleteWatchFileFolder;
end;
